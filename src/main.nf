#!/usr/bin/env nextflow

/*
Copyright (c) 2026 The Hiller Lab at the Senckenberg Gessellschaft für Naturforschung
Distributed under the terms of the Apache License, Version 2.0.
*/

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    xorf

    end-to-end robust and comprehensive ORF prediction pipeline
    Authors: Alejandro Gonzales-Irribarren, Michael Hiller

    GitHub:  https://github.com/hillerlab/xorf
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    HELP
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

if (params.help) {
    log.info """
    xorf v${workflow.manifest.version}
    End-to-end robust and comprehensive ORF prediction pipeline

    Authors: ${workflow.manifest.author}
    Github:  ${workflow.manifest.homePage}

    Usage (full run):
        nextflow run main.nf \\
            --regions        /path/to/regions.{bed, gtf, gff} \\
            --sequence       /path/to/genome.{fa, 2bit, fa.gz} \\
            --outdir         results/ \\
            -profile         apptainer,slurm

    Pass all parameters from a JSON file (replaces old --params_from_file):
        nextflow run main.nf -params-file my_params.json

    Required parameters (full run + fill/clean):
        --regions            PATH    Path to genomic regions (BED, GTF, or GFF)
        --sequence           PATH    Path to genome sequence (FASTA or 2bit)

    Optional parameters (common):
        --outdir              PATH    Output directory [default: ./results]
        --engine              STRING  BLAST engine: diamond or mmseqs2 [default: diamond]
        --custom_database     PATH    Path to custom database [default: null]:
                                      diamond: .dmnd/.dmnd.gz replaces the default database;
                                      fasta (.fa/.fasta/.fa.gz/.fasta.gz) is appended to raw_database
                                      mmseqs2: fasta is appended to raw_database; .dmnd is rejected
        --raw_database        URL     Raw protein FASTA (SwissProt). Used for fasta custom_database
                                      and as the default when --engine mmseqs2 [default: UniProt/SwissProt]
        --chunk_size          INT     Chunk size for parallel processing [default: 20]
        --predict_keep_raw    BOOL      Keep raw predictions [default: false]
        --selenocysteine_sites PATH      Selenocysteine masking [default: null]
        --predict_min_score_max_predictions FLOAT   Minimum score for ORF predictions [default: 0.50]
        --predict_max_predictions INT  Maximum number of ORF predictions [default: 3]
        --skip_netstart       BOOL      Skip netstart [default: false]
        --run_only_on         BOOL      Run only on masked or unmasked sequences [default: false]
        --run_only_mode       STRING    Run only on masked or unmasked sequences [default: null; options: mask, unmask]
        --run_only_target     STRING    Run only on masked or unmasked sequences [default: null; options: intersect, exclude]

    Profiles:
        local       Run on local machine (default)
        slurm       Submit jobs to SLURM cluster
        conda       Use conda environments
        apptainer   Use Apptainer containers
        singularity Use Singularity containers
        docker      Use Docker containers
        test        Run with bundled test data

    Use --help to show this message.
    """.stripIndent()
    System.exit(0)
}

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    IMPORT LOCAL MODULES/SUBWORKFLOWS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

include { EMAIL }        from './modules/email/main.nf'
include { XORF as MAIN }         from './subworkflows/xorf/main.nf'
include { WGET as WGET_SAMBA_WEIGHTS } from './modules/wget/main.nf'
include { UNTAR } from './modules/untar/main.nf'
include { WGET as WGET_PROTEIN_DATABASE } from './modules/wget/main.nf'
include { WGET as WGET_MMSEQS_FASTA } from './modules/wget/main.nf'
include { GUNZIP as GUNZIP_DATABASE } from './modules/gunzip/main.nf'
include { FASTA_MERGE } from './modules/diamond/merge/main.nf'
include { FASTA_MERGE as FASTA_MERGE_MMSEQS } from './modules/diamond/merge/main.nf'
include { DIAMOND_MAKEDB } from './modules/diamond/makedb/main.nf'
include { MMSEQS_CREATEDB } from './modules/mmseqs/createdb/main.nf'

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    VALIDATION
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

def validateRun() {
    def errors = []
    if (!params.regions)   errors << "  --regions is required"
    if (!params.sequence)  errors << "  --sequence is required"
    if (!(params.engine in ['diamond', 'mmseqs2'])) {
        errors << "  --engine must be 'diamond' or 'mmseqs2' (got: ${params.engine})"
    }
    if (params.engine == 'mmseqs2' && params.custom_database && isDiamondDb(params.custom_database)) {
        errors << "  --engine mmseqs2 cannot use a diamond .dmnd database; pass FASTA or omit custom_database"
    }

    if (errors) {
        log.error "Parameter validation failed:\n${errors.join('\n')}"
        System.exit(1)
    }
}

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    LOCAL SUBWORKFLOWS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

workflow PIPELINE_COMPLETION {

    take:
      email
      email_on_fail
      plaintext_email
      outdir
      use_mailx
      files
      counts
      ch_versions

    main:

      if (params.sent_email) {
          EMAIL (
              email,
              email_on_fail,
              plaintext_email,
              outdir,
              use_mailx,
              files,
              counts,
              ch_versions
          )
      }

      workflow.onError {
          log.error "ERROR: Pipeline failed!"
          log.error "ERROR: Please check the following error message:\n${workflow.errorMessage}"
          log.error "ERROR: Refer to github issues: https://github.com/alejandrogzi/HLorf/issues"
      }

      workflow.onComplete {
          log.info "\nPipeline completed successfully!"
      }
}

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    RUN MAIN WORKFLOW
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

workflow {
    XORF()
}

workflow XORF {
    
    log.info """
    > xorf v${workflow.manifest.version}
    > End-to-end robust and comprehensive ORF prediction pipeline
    > The Hiller Lab at the Senckenberg Research Institute

    Authors: ${workflow.manifest.author}
    Github:  ${workflow.manifest.homePage}

      Regions:   ${params.regions}
      Sequence:  ${params.sequence}
      Database:  ${params.database} (custom: ${params.custom_database}, engine: ${params.engine})
      Outdir:    ${params.outdir}
      Run only:  ${params.run_only_on} (mode: ${params.run_only_mode}, target: ${params.run_only_target})
      Profile:   ${workflow.profile}
    """.stripIndent()

    /*
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        VALIDATE INPUTS
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    */

    validateRun()

    /*
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        SAMBA WEIGHTS
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    */

    ch_samba_weights = Channel.empty()
    if (params.samba_local_weights) {
        ch_samba_weights = Channel.value(
          file(params.samba_local_weights, checkIfExists: true)
        ).map { path -> [ [id : path.baseName ], path ] }
    } else {
        WGET_SAMBA_WEIGHTS(
          Channel.value(
            params.samba_weights
          ).map { url -> [ [id : url.tokenize('/')[-1]], url ] }
        )
        ch_samba_weights = WGET_SAMBA_WEIGHTS.out.outfile
    }

    /*
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        PROTEIN DATABASE
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    */

    ch_database = Channel.empty()
    ch_database_versions = Channel.empty()
    if (params.engine == 'mmseqs2') {
      // INFO: mmseqs2 never uses the zenodo diamond .dmnd artifact.
      // Default target is params.raw_database (SwissProt FASTA).
      if (params.custom_database && !isProteinFasta(params.custom_database)) {
          error """
          ERROR: custom_database extension not recognized for --engine mmseqs2.
          Please provide a FASTA file:
            .fa/.fasta/.fa.gz/.fasta.gz -> appended to raw_database and indexed with mmseqs
          """.stripIndent()
      }

      WGET_MMSEQS_FASTA(
          Channel.value(params.raw_database)
          .map { it -> [ [ id: 'uniprot_sprot.fasta.gz' ], it ] }
      )

      ch_mmseqs_fasta = WGET_MMSEQS_FASTA.out.outfile
          .map { meta, fasta -> [ [ id: 'mmseqs_db' ], fasta ] }
      ch_database_versions = Channel.empty()

      if (params.custom_database) {
          FASTA_MERGE_MMSEQS(
              Channel.fromPath(params.custom_database, checkIfExists: true)
                .map { it -> [ [ id: it.baseName ], it ] }
                .combine(WGET_MMSEQS_FASTA.out.outfile.map { meta, raw -> raw })
          )
          ch_mmseqs_fasta = FASTA_MERGE_MMSEQS.out.fasta
              .map { meta, fasta -> [ [ id: 'merged_database' ], fasta ] }
          ch_database_versions = FASTA_MERGE_MMSEQS.out.versions
      }

      MMSEQS_CREATEDB(ch_mmseqs_fasta)
      ch_database = MMSEQS_CREATEDB.out.db.map { meta, it -> it }
      ch_database_versions = ch_database_versions.mix(MMSEQS_CREATEDB.out.versions)
    } else if (params.custom_database) {
      // INFO: .dmnd / .dmnd.gz -> replaces the default database entirely
      if (params.custom_database.endsWith('.dmnd')) {
          ch_database = Channel.fromPath(params.custom_database, checkIfExists: true)
      } else if (params.custom_database.endsWith('.dmnd.gz')) {
          GUNZIP_DATABASE(
              Channel.value(
                  [ [id: params.custom_database.tokenize('/')[-1]], params.custom_database ]
              )
          )
          GUNZIP_DATABASE.out.gunzip
            .map { meta, it -> it }
            .set { ch_database }
      // INFO: .fa / .fasta / .fa.gz / .fasta.gz -> downloaded raw database + custom
      // sequences are merged, then reindexed with DIAMOND_MAKEDB
      } else if (isProteinFasta(params.custom_database)) {
          WGET_PROTEIN_DATABASE(
              Channel.value(params.raw_database)
              .map { it -> [ [ id: 'uniprot_sprot.fasta.gz' ], it ] }
          )
          FASTA_MERGE(
              Channel.fromPath(params.custom_database, checkIfExists: true)
                .map { it -> [ [ id: it.baseName ], it ] }
                .combine(WGET_PROTEIN_DATABASE.out.outfile.map { meta, raw -> raw })
          )
          DIAMOND_MAKEDB(
              FASTA_MERGE.out.fasta.map { meta, fasta -> [ [ id: 'merged_database' ], fasta ] },
              [],
              [],
              []
          )
          DIAMOND_MAKEDB.out.db
            .map { meta, it -> it }
            .set { ch_database }
          ch_database_versions = FASTA_MERGE.out.versions.mix(DIAMOND_MAKEDB.out.versions)
      } else {
          error """
          ERROR: custom_database extension not recognized.
          Please provide a custom database in one of these formats:
            .dmnd/.dmnd.gz   -> replaces the default database entirely
            .fa/.fasta/.fa.gz/.fasta.gz -> appended to the default SwissProt database and reindexed
          """.stripIndent()
      }
    } else {
      WGET_PROTEIN_DATABASE(
        Channel.value(params.database)
        .map { it -> [ [ id: 'uniprot_sprot.tar.gz' ], it ] }
      )
      UNTAR(WGET_PROTEIN_DATABASE.out.outfile)
      ch_database = UNTAR.out.contents.map { meta, it -> it }
    }

    /*
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        MAIN WORKFLOW
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    */

    MAIN (
       Channel.fromPath(params.regions).map { it -> [ [id: it.baseName, chr: randomHash()], it ] },
       Channel.fromPath(params.sequence),
       ch_database,
       params.outdir,
       params.chunk_size,
       ch_samba_weights,
       params.predict_keep_raw,
       params.selenocysteine_sites,
       params.skip_netstart,
       params.rename_deactivate,
       params.do_polishing,
       params.skip_joined_concat,
       params.run_only_on,
       params.run_only_mode,
       params.run_only_target,
       ch_database_versions
    )

    /*
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
        PIPELINE COMPLETION
    ~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    */

    PIPELINE_COMPLETION (
        params.email_to,
        params.email_on_fail,
        params.plaintext_email,
        params.outdir,
        params.use_mailx,
        MAIN.out.files,
        MAIN.out.counts,
        MAIN.out.versions
    )
}

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    FUNCTIONS
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

def randomHash() {
    def chars = ('0'..'9') + ('a'..'z') + ('A'..'Z')
    def random = new Random()
    return (1..6).collect { chars[random.nextInt(chars.size())] }.join()
}

def isProteinFasta(path) {
    def p = path.toString().toLowerCase()
    return p.endsWith('.fa') || p.endsWith('.fasta') || p.endsWith('.fa.gz') || p.endsWith('.fasta.gz')
}

def isDiamondDb(path) {
    def p = path.toString().toLowerCase()
    return p.endsWith('.dmnd') || p.endsWith('.dmnd.gz')
}

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    COMPLETION HANDLER
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/

workflow.onComplete {
    if (workflow.success) {
        def intermediate_dir = new File(params.outdir as String, '02_merged')
        def intermediate_beds = intermediate_dir.exists() ? (intermediate_dir.listFiles()?.findAll { it.name.endsWith('.bed') } ?: []) : []
        
        def results_dir = new File(params.outdir as String, '04_results')
        def final_beds = results_dir.exists() ? (results_dir.listFiles()?.findAll { it.name.endsWith('.bed') } ?: []) : []
        log.info "Pipeline completed successfully!"
        if (final_beds) {
            log.info "Final predictions: ${final_beds.collect { it.toString() }.join(', ')}"
        } else {
            if (intermediate_beds) {
                log.info "Final predictions: ${intermediate_beds.collect { it.toString() }.join(', ')}"
            }

            log.warn "Pipeline reported success but final bed file was not produced - check that all steps ran"
        }
        log.info "Run time   : ${workflow.duration}"
    } else {
        log.error "Pipeline FAILED — ${workflow.errorMessage}"
    }
}

workflow.onError {
    log.error "Pipeline error: ${workflow.errorMessage}"
}

/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    THE END
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
*/
